# Author: Hong Jen Yee (PCMan) <pcman.tw@gmail.com>
# Copyright (C) 2011
# License: MIT

cmake_minimum_required (VERSION 2.6) # required version of cmake
project (lxpanel2)    # set project name

# include our own cmake modules
list (APPEND CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake)

# configure a header file to pass some of the build settings
# to the source code, just like the config.h generated by autotools
configure_file ("config.h.in" "${PROJECT_BINARY_DIR}/config.h")
include_directories("${PROJECT_BINARY_DIR}")
add_definitions(-DHAVE_CONFIG_H)

# find required libraries and packages ---------------------------------

find_package(PkgConfig)  #look for pkg-config

find_package(Vala REQUIRED)
# ensure_vala_version("0.14.0" MINIMUM)
include(UseVala)

# define GNU install directories, such as bindir, libexecdir, sysconfdir
set(CMAKE_INSTALL_SYSCONFDIR "/etc" CACHE PATH "Location for system configuration files")
include(GNUInstallDirs)

# use pkg-config to find gtk+ 3.0
pkg_check_modules(GTK REQUIRED gtk+-3.0)
# find glib tools
find_program(GLIB_GENMARSHAL
	NAMES glib-genmarshal
	DOC "glib-genmarshal executable"
)
mark_as_advanced(GLIB_GENMARSHAL)

find_program(GLIB_MKENUMS
	NAMES glib-mkenums
	DOC "glib-mkenums executable"
)
mark_as_advanced(GLIB_MKENUMS)

pkg_check_modules(WNCK REQUIRED libwnck-3.0)

pkg_check_modules(X11 REQUIRED x11)

pkg_check_modules(GTOP REQUIRED libgtop-2.0)

pkg_check_modules(MENU_CACHE REQUIRED libmenu-cache)

# test for gcc and add special compiler flags, such as -g -Wall
if(CMAKE_COMPILER_IS_GNUCC)
    set (GCC_FLAGS "-Wall -Werror-implicit-function-declaration")
    set (DEBUG_FLAGS "-g")
endif(CMAKE_COMPILER_IS_GNUCC)

# setup translations ---------------------------------------------------
set (GETTEXT_PACKAGE "lxpanel2")
# find intltools
find_package(Intltool)

# build the po files with intltool
intltool_create_translations(
	GETTEXT_PACKAGE	"${GETTEXT_PACKAGE}"
	PO_DIRECTORY po
)

# an option to turn off NLS
option(ENABLE_NLS "Build translations" ON)

# global compiler definitions applied to all files
# add_definitions()

# add sub directories --------------------------------------------------
add_subdirectory(src)
add_subdirectory(data)

# building tarball with CPack ------------------------------------------
# To create a source distribution, type:
# make package_source
include (InstallRequiredSystemLibraries)
set (CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/COPYING")
set (CPACK_PACKAGE_VENDOR "")
set (CPACK_PACKAGE_VERSION_MAJOR "0")
set (CPACK_PACKAGE_VERSION_MINOR "1")
set (CPACK_PACKAGE_VERSION_PATCH "0")
set (CPACK_GENERATOR TBZ2)
set (CPACK_SOURCE_GENERATOR TBZ2)
set (CPACK_SOURCE_IGNORE_FILES /build/;.gitignore)
set(CPACK_SET_DESTDIR ON)
# set (CPACK_SOURCE_PACKAGE_FILE_NAME lxpanel2-0.1.0.tar.gz)
# set (CPACK_SOURCE_OUTPUT_CONFIG_FILE )
include (CPack)
