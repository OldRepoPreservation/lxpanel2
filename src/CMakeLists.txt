# Author: Hong Jen Yee (PCMan) <pcman.tw@gmail.com>
# Copyright (C) 2011
# License: MIT

# global compiler definitions applied to all files
# add_definitions()

include_directories(
    ${GTK_INCLUDE_DIRS}
    ${WNCK_INCLUDE_DIRS}
    ${X11_INCLUDE_DIRS}
    ${GTOP_INCLUDE_DIRS}
    ${MENU_CACHE_INCLUDE_DIRS}
    egg
    applets/na-tray
)

# link_directories() cmake command only works if put before build target.
link_directories(
    ${GTK_LIBRARY_DIRS}
    ${WNCK_LIBRARY_DIRS}
    ${X11_LIBRARY_DIRS}
    ${GTOP_LIBRARY_DIRS}
    ${MENU_CACHE_LIBRARY_DIRS}
)

vala_precompile(VALA_C
	# core files
	lxpanel2.vala
	panel.vala
	applet.vala
	button.vala
	menu-button.vala
	drawer.vala
	popup.vala
	upower.vala
	gmarkup-dom.vala
	utils.vala
	module.vala
	# built-in applets
	applets/app-menu-applet.vala
	applets/battery-applet.vala
	applets/blank-applet.vala
	applets/clock-applet.vala
	applets/launchbar-applet.vala
	applets/logout-applet.vala
	applets/netstatus-applet.vala
	applets/pager-applet.vala
	applets/show-desktop-applet.vala
	applets/task-list-applet.vala
	applets/systray-applet.vala
	applets/places-applet.vala
PACKAGES
	config
	gtk+-3.0
	gdk-3.0
    gio-2.0
    gio-unix-2.0
    gmodule-2.0
    posix
	libwnck-3.0
	x11
	eggwrapbox
	gtop-2.0
	menu-cache
	na-tray
GENERATE_VAPI
    lxpanel2
GENERATE_HEADER
	lxpanel2
OPTIONS
	--thread
	--vapidir="${CMAKE_CURRENT_SOURCE_DIR}/vapi"
#	--disable-assert
	--disable-warnings
#	--quiet
)

# define build targets
add_executable(lxpanel2
	# all vala code
	${VALA_C}
	gtk-run.c
	# NaTray code from gnome-panel, for systray
	applets/na-tray/na-tray.c
	applets/na-tray/na-tray-child.c
	applets/na-tray/na-tray-manager.c
	applets/na-tray/fixedtip.c
	na-marshal.c # generated by glib-genmarshal
	# EggWrapBox from libegg
	egg/eggwrapbox.c
	egg/eggwrapbox-enums.c
)

# generate na-marshal.[ch]
# FIXME: we should make this a cmake module for ease of reuse later
add_custom_command(
	OUTPUT na-marshal.h na-marshal.c
	DEPENDS applets/na-tray/na-marshal.list
	WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/applets/na-tray
	COMMAND ${GLIB_GENMARSHAL} --header --prefix=_na_marshal na-marshal.list
		> ${CMAKE_CURRENT_BINARY_DIR}/na-marshal.h
	COMMAND ${GLIB_GENMARSHAL} --body --prefix=_na_marshal na-marshal.list
		> ${CMAKE_CURRENT_BINARY_DIR}/na-marshal.c
)

# link with external libraries
target_link_libraries(lxpanel2
    ${GTK_LIBRARIES}
    ${WNCK_LIBRARIES}
    ${X11_LIBRARIES}
    ${GTOP_LIBRARIES}
    ${MENU_CACHE_LIBRARIES}
)

# add debug and some special gcc compiler flags. CMake does not support
# global COMPILE_FLAGS
set_property(TARGET lxpanel2 APPEND
    PROPERTY COMPILE_FLAGS "${GCC_FLAGS} ${DEBUG_FLAGS}"
)

# add proper defines for the target
set_property(TARGET lxpanel2 APPEND
    PROPERTY COMPILE_DEFINITIONS
    GETTEXT_PACKAGE="${GETTEXT_PACKAGE}"
	WNCK_I_KNOW_THIS_IS_UNSTABLE # for using libwnck
)

# install the compiled program
install(TARGETS lxpanel2 DESTINATION bin)

# install library header files
install(FILES
	"${CMAKE_CURRENT_BINARY_DIR}/lxpanel2.h"
	DESTINATION include/lxpanel2
)

# FIXME: where should we install the vapi file?
#install(FILES
#	"${CMAKE_CURRENT_BINARY_DIR}/lxpanel2.vapi"
#	DESTINATION include/lxpanel2
#)

# built dynamic plugins/applets
vala_precompile(VALA_PLUGIN_C
	applets/sample-dynamic-applet.vala
PACKAGES
	config
	gtk+-3.0
	gdk-3.0
    gio-2.0
    gio-unix-2.0
    gmodule-2.0
    posix
	libwnck-3.0
	x11
	eggwrapbox
	gtop-2.0
	menu-cache
	na-tray
	lxpanel2
OPTIONS
	--thread
	--vapidir="${CMAKE_CURRENT_SOURCE_DIR}/vapi"
	--vapidir="${CMAKE_CURRENT_BINARY_DIR}"
	--disable-warnings
)

add_library(sample MODULE
	${VALA_PLUGIN_C}
)

# link with external libraries
target_link_libraries(sample
    ${GTK_LIBRARIES}
    ${WNCK_LIBRARIES}
    ${X11_LIBRARIES}
    ${GTOP_LIBRARIES}
    ${MENU_CACHE_LIBRARIES}
)

# add debug and some special gcc compiler flags. CMake does not support
# global COMPILE_FLAGS
set_property(TARGET sample APPEND
    PROPERTY COMPILE_FLAGS "${GCC_FLAGS} ${DEBUG_FLAGS}"
)

# do not prepend output filename with "lib"
set_target_properties(sample
	PROPERTIES PREFIX ""
)

# add proper defines for the target
set_property(TARGET sample APPEND
    PROPERTY COMPILE_DEFINITIONS
    GETTEXT_PACKAGE="${GETTEXT_PACKAGE}"
   	WNCK_I_KNOW_THIS_IS_UNSTABLE # for using libwnck
)

# install the compiled program
install (TARGETS sample DESTINATION lib/lxpanel2/applets)

